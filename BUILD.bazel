load("@rules_foreign_cc//foreign_cc:configure.bzl", "configure_make_variant")
load("//bazel:do_nothing.bzl", "do_nothing")

filegroup(
    name = "all",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)

alias(
    # The `configure_make_variant` wrapper around `configure_make` doesn't
    # pass the 'visibility' setting correctly; it gets lost. As a
    # workaround we use an alias with the correct visibility setting.
    name = "jemalloc",
    actual = select({
        # For now jemalloc library successfully builds on macOS
        # and Linux. In future we are going to make it build on
        # Windows too.
        # https://github.com/3rdparty/bazel-rules-jemalloc/tree/ArthurBandaryk.jemalloc-windows
        # That's why we do nothing on Windows.
        "@bazel_tools//src/conditions:windows": "do_nothing_on_windows",
        "//conditions:default": "jemalloc_preinstalled_make",
    }),
    visibility = ["//visibility:public"],
)

configure_make_variant(
    name = "jemalloc_preinstalled_make",
    autoconf = True,
    configure_in_place = True,
    env = select({
        # Without this option on macOS we will have the following issue:
        # .../libtool: no output file specified (specify with -o output).
        "@bazel_tools//src/conditions:darwin": {"AR": ""},
        "//conditions:default": {},
    }),
    lib_source = ":all",
    out_static_libs = select({
        "@bazel_tools//src/conditions:windows": [
            "libjemalloc_a.lib",
        ],
        "//conditions:default": ["libjemalloc.a"],
    }),
    # We need to use the system-default make; the version of make packaged
    # with `rules_foreign_cc` (4.3) segfaults on GitHub's Actions workers.
    # The version that is pre-installed on those workers (3.81) works fine.
    toolchain = "@rules_foreign_cc//toolchains:preinstalled_make_toolchain",
)

do_nothing(
    name = "do_nothing_on_windows",
)
